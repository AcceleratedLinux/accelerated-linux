& This command macro creates the appropriate subdirectory
& for the specified processor type and then runs the
& build macro in that subdirectory to create the perl
& executable program module file.
& Written 99-02-03 by Paul Green (Paul_Green@stratus.com)
& Modified 00-10-24 by Paul Green
&
&begin_parameters
     cpu       option(-processor)name,allow(mc68020,i80860,pa7100,pa8000),=pa7100
     recompile switch(-recompile),=1
     rebind    switch(-rebind),=1
     module    option(-module)module_name,='(current_module)'
     tgt_mod   option(-target_module)module_name,='(current_module)'
     version   option(-version)name,allow(alpha,ga),=ga
     compiler  option(-compiler)name,allow(cc,gcc)=cc
&end_parameters
&echo command_lines
&
&if &recompile&
&then &set_string recompile -recompile
&else &set_string recompile -no_recompile
&
&if &rebind&
&then &set_string rebind -rebind
&else &set_string rebind -no_rebind
&
&if &cpu& = mc68020
&then &set_string obj ''
&if &cpu& = i80860
&then &set_string obj .860
&if &cpu& = pa7100
&then &set_string obj .7100
&if &cpu& = pa8000
&then &set_string obj .8000
&
&if &compiler& = gcc & &version& = alpha
&then &do
 &display_line GCC is not supported by the alpha version of POSIX support.
 &return e$translation_failed
&end
&if &cpu& = mc68020 & &version& = ga
&then &do
 &display_line The mc68020 CPU is not supported by the GA version of POSIX support.
 &return e$translation_failed
&end
&if &cpu& = i80860 & &version& = ga
&then &do
 &display_line The i80860 CPU is not supported by the GA version of POSIX support.
 &return e$translation_failed
&end
&if &cpu& = pa8000 & &compiler& = gcc
&then &do
 &display_line The pa8000 CPU is not supported by the GNU GCC compiler.
 &return e$translation_failed
&end
&if ^ (exists obj&obj& -directory)
&then !create_dir obj&obj&
&
&if ^ (exists obj&obj&>build.out)
&then !create_file obj&obj&>build.out ; set_implicit_locking obj&obj&>build.out
&
!configure_perl -version &version&
&
!change_current_dir obj&obj&
!start_process (string <build -processor &cpu& &recompile& &rebind& &+
     -target_module &tgt_mod& -version &version& -compiler &compiler&) &+
     -module &module&
!change_current_dir <
