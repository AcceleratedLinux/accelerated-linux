'begin'
	'comment'
		examine some kinds of for-loops
	;

	'integer' i, sum;
	'real' x, y;

	sum := 0;
	'for' i := 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 'do'
		sum := sum + i;
	'if' sum = 55 'then' vprint ("1. ok.") 'else' vprint ("1. error", sum);

	sum := 0;
	'for' i := 1 'step' 1 'until' 10 'do'
		sum := sum + i;
	'if' sum = 55 'then' vprint ("2. ok.") 'else' vprint ("2.	error", sum);

	sum := 0;
	'for' i := 10 'step' -1 'until' 1 'do'
		sum := sum + i;
	'if' sum = 55 'then' vprint ("3. ok.") 'else' vprint ("3.	error:", sum);

	sum := 0;
	'for' i := 1, i + 1 'while' i <= 10 'do'
		sum := sum + i;
	'if' sum = 55 'then' vprint ("4. ok.") 'else' vprint ("4.	error:", sum);

	sum := 0;
	'for' i := 1, 2, 3, i + 1 'while' i <= 7, 8 'step' 1 'until' 10 'do'
		sum := sum + i;
	'if' sum = 55 'then' vprint ("5. ok.") 'else' vprint ("5.	error:", sum);

'end'
